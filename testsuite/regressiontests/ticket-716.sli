/*
 *  ticket-716.sli
 *
 *  This file is part of NEST.
 *
 *  Copyright (C) 2004 The NEST Initiative
 *
 *  NEST is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 2 of the License, or
 *  (at your option) any later version.
 *
 *  NEST is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with NEST.  If not, see <http://www.gnu.org/licenses/>.
 *
 */

/** @BeginDocumentation

Name: testsuite::ticket-716 - stdp dopamine synapse changes weight in the absence of presynaptic spikes

Synopsis: (ticket-716) run -> NEST exits if test fails

Description:
Ensure that the weight of an stdp dopamine synapse is constant in the absense of presynaptic spiking.

Author: Susanne Kunkel, 2013-04-11
 */

(unittest) run
/unittest using

M_ERROR setverbosity

ResetKernel

/vol /volume_transmitter Create def
/stdp_dopamine_synapse << /vt vol >> SetDefaults

/n_pre /parrot_neuron Create def % does not fire
/n_post /parrot_neuron Create def
/n_dopa /parrot_neuron Create def

/sg_post /spike_generator << /spike_times [0.5 1.1 3.4] >> Create def
/sg_dopa /spike_generator << /spike_times [1.4 2.3 4.6] >> Create def

n_pre n_post /stdp_dopamine_synapse Connect
sg_dopa n_dopa Connect
n_dopa vol Connect
sg_post n_post Connect

/w0 << /synapse_model /stdp_dopamine_synapse >> GetConnections 0 get GetStatus /weight get def
10.0 Simulate
/w1 << /synapse_model /stdp_dopamine_synapse >> GetConnections 0 get GetStatus /weight get def

{ w0 w1 sub abs 1e-13 leq } assert_or_die

endusing
