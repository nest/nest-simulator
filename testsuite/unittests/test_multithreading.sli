/*
 *  test_multithreading.sli
 *
 *  This file is part of NEST.
 *
 *  Copyright (C) 2004 The NEST Initiative
 *
 *  NEST is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 2 of the License, or
 *  (at your option) any later version.
 *
 *  NEST is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with NEST.  If not, see <http://www.gnu.org/licenses/>.
 *
 */

/** @BeginDocumentation
Name: testsuite::test_multithreading - sli script for several tests regarding multithreading

Synopsis: (test_multithreading) run

Description:
This is a simple testscript to test if multithreading is working
correctly. The following things are tested:
  * Does setting the number of threads to x result in x threads?
  * Does ResetKernel reset the number of threads to 1?
  * Does default node distribution (modulo) work as expected?
  * Are spikes transmitted between threads as expected?

The data collection over threads is tested in a separate script. See
SeeAlso key below.

SeeAlso:testsuite::test_multithreading_devices

Author: Jochen Martin Eppler
FirstVersion: July 2008 
*/


(unittest) run
/unittest using

skip_if_not_threaded

/threads 4 def

% check if setting the number of threads works
<< /local_num_threads threads >> SetKernelStatus
GetKernelStatus /local_num_threads get threads eq assert_or_die

% check ResetKernel
ResetKernel
GetKernelStatus /local_num_threads get 1 eq assert_or_die

% use 4 threads again
<< /local_num_threads threads >> SetKernelStatus

% check if modulo node distribution works
/iaf_psc_alpha 4 Create {
  dup threads mod exch [ /vp ] get eq assert_or_die
} forall


% check if spikes are transmitted between threads
ResetKernel
/t_spike 1. def 
/delay 1. def
<< /local_num_threads threads >> SetKernelStatus
/sg /spike_generator << /spike_times [ t_spike ] >> Create def
/pA /parrot_neuron threads Create def
/pB /parrot_neuron threads Create def
/sr /spike_recorder Create def

sg pA /all_to_all << /delay delay >> Connect
pA pB /all_to_all << /delay delay >> Connect
pB sr Connect

t_spike delay 3 mul add Simulate

% expectation: each parrot in pA sends one spike to each parrot in pB,
%              thus in total threads**2 spikes in pB, all at t_spike + 2*delay
sr /events get /times get cva
[ threads dup mul ] { ; t_spike delay 2 mul add } Table
eq assert_or_die
